plugins {
	id "com.bmuschko.docker-java-application" version "3.0.6"
}
apply plugin: 'application'
apply plugin: 'eclipse'

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'windows-1252'
compileTestJava.options.encoding = 'windows-1252'

mainClassName = 'de.mwvb.oceanground.OceanGroundApp'
distZip.enabled = false

dependencies {
	compile 'com.github.SoltauFintel.maja:maja-auth-mongo:0.2.0'
	compile 'com.github.docker-java:docker-java:3.0.9'
	
	testCompile 'junit:junit:4.12'
}

repositories {
	maven { url 'https://jitpack.io' }
	mavenCentral()
}

startScripts {
	doLast { // Remove too-long-classpath and use wildcard (works for Java 6 and above only)
		windowsScript.text = windowsScript.text.replaceAll('set CLASSPATH=.*', 'set CLASSPATH=.;%APP_HOME%/lib/*')
	}
}

distTar {
	if (target == 'base') {
		excludes = [ jar.archiveName ]
	} else if (target == 'app') {
		includes = [ jar.archiveName ]
	}
}

docker {
	url = 'tcp://192.168.99.100:2376'
	certPath = new File(System.properties['user.home'], '.docker/machine/machines/default')
	javaApplication {
		maintainer = 'Marcus Warm'
		ports = [ 9032 ]
		def imagename = 'mwvb.de:5000/oceanground'
		if (target == 'base') {
			baseImage = 'openjdk:8u121-jre'
			tag = imagename + 'base'
		} else {
			baseImage = imagename + 'base'
			tag = imagename
		}
	}
}
